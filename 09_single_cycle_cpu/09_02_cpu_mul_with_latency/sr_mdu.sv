//
//  schoolRISCV - small RISC-V CPU
//
//  Originally based on Sarah L. Harris MIPS CPU
//  & schoolMIPS project.
//
//  Copyright (c) 2017-2020 Stanislav Zhelnio & Aleksandr Romanov.
//
//  Modified in 2024 by Yuri Panchul & Mike Kuskov
//  for systemverilog-homework project.
//

`include "sr_cpu.svh"

module sr_mdu
# (
    parameter n_delay = 2
)
(
    input               clk,
    input               rst,

    input               i_vld,
    input        [31:0] srcA,
    input        [31:0] srcB,
    output              o_vld,
    output logic [31:0] result,
    output              busy
);

    shift_register # (.width(32), .depth(n_delay)) res_shift_reg
    (
        .clk              (clk),
        .in_data  (srcA * srcB),
        .out_data (result)
    );

    logic [n_delay - 1:0] i_vld_shift_reg;

    always_ff @ (posedge clk)
        if (rst)
            i_vld_shift_reg <= '0;
        else
        begin
            i_vld_shift_reg [0] <= i_vld;

            for (int i = 1; i < n_delay; i ++)
                i_vld_shift_reg [i] <= i_vld_shift_reg [i - 1];
        end

    assign o_vld = i_vld_shift_reg [n_delay - 1];
    assign busy = |i_vld_shift_reg;
    
endmodule

//----------------------------------------------------------------------------

module shift_register
# (
    parameter width = 8, depth = 8
)
(
    input                clk,
    input  [width - 1:0] in_data,
    output [width - 1:0] out_data
);
    logic [width - 1:0] data [0:depth - 1];

    always_ff @ (posedge clk)
    begin
        data [0] <= in_data;

        for (int i = 1; i < depth; i ++)
            data [i] <= data [i - 1];
    end

    assign out_data = data [depth - 1];

endmodule

